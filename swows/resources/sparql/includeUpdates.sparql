PREFIX df:      <http://www.swows.org/dataflow#>
PREFIX swi:     <http://www.swows.org/instance#>
PREFIX sp:      <http://spinrdf.org/sp#>
PREFIX spx:   <http://www.swows.org/spinx#>
PREFIX bn:      <http://www.swows.org/instance/.well-known/genid/>
PREFIX rdfs:   <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
   ?s ?p ?o .
   ?updatableGraph sp:query ?updateQuery .
   ?sG ?pG ?oG .
   
   ?updateQuery sp:where ?newWhereElement .
   ?newWhereElement
      a sp:Element, sp:NamedGraph ;
      sp:graphNameNode ?mainGraph ;
      spx:element ?mainElement .
   ?innerNamedGraph sp:graphNameNode ?innerGraph .
}
WHERE {
   
   ?s ?p ?o .

    FILTER (
      ( NOT EXISTS { ?s a df:UpdatableGraph }
        || (?p NOT IN (df:config, df:input)) )
      && NOT EXISTS { 
          ?sP
            a df:UpdatableGraph ;
            df:input/df:namedInput? ?s }
    ) .

   OPTIONAL {
      
      ?updatableGraph
      a df:UpdatableGraph ;
      df:config ?importedGraph .

      OPTIONAL {
         ?updatableGraph df:input ?inputDataset .
         ?inputDataset a df:InlineDataset .
         {
            ?inputDataset df:input ?mainGraph .
            BIND ( sp:default AS ?mainGraphName ) .
         } UNION {
            ?inputDataset df:namedInput ?mainGraphInput .
            ?mainGraphInput df:input ?mainGraph .
               ?mainGraphInput df:id ?mainGraphName .
         }
         OPTIONAL {
            ?inputDataset df:namedInput ?innerGraphInput .
            ?innerGraphInput df:input ?innerGraph .
               ?innerGraphInput df:id ?innerGraphName .
         }
      }

      ?importedGraph
      a df:ImportedGraph ;
      df:uri ?graphName .
      GRAPH ?graphName {
    
         ?sG ?pG ?oG .
         FILTER (
             ?sG != swi:GraphRoot
             && ( !EXISTS{?sG a sp:NamedGraph}
                   || ?pG != sp:graphNameNode )
             && ( !EXISTS{ swi:GraphRoot ?memberP2 ?sG }
                   || ?pG != sp:where
                   || EXISTS {?oR a sp:NamedGraph} ) ) .
      
          swi:GraphRoot ?memberP ?updateQuery .
      
         {
            ?updateQuery sp:where ?whereElement .
            FILTER (NOT EXISTS{?whereElement a sp:NamedGraph}) .
            BIND( URI( CONCAT( STR(bn:where_), MD5( STR(?whereElement) ) ) ) AS ?newWhereElement ) .
            BIND( sp:default AS ?mainGraphName ) .
            BIND( ?whereElement AS ?mainElement ) .
         } UNION {
            ?updateQuery sp:where ?newWhereElement .
            ?newWhereElement
            a sp:NamedGraph ;
            sp:graphNameNode ?mainGraphName ;
            spx:element ?mainElement .
         }
         OPTIONAL {
            ?innerNamedGraph
            a sp:NamedGraph ;
            sp:graphNameNode ?innerGraphName ;
            spx:element ?innerElement .
         }
      }
   }
}